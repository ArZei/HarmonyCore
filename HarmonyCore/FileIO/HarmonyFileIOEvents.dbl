import System
import System.Collections.Generic
import System.Text
import System.Diagnostics.Tracing


namespace Harmony.Core.FileIO

	{EventSource(Name = "HarmonyCore-FileIO-Metrics")}
	public sealed class HarmonyFileIOMetricsEventSource extends EventSource
        
		private ioopsCounter, @EventCounter 
		private recordsCounter, @EventCounter 
		private dynamicCounters, @Dictionary<string, EventCounter>, new Dictionary<string, EventCounter>()

		public static Log, @HarmonyFileIOMetricsEventSource, new HarmonyFileIOMetricsEventSource()


		public class Keywords
			public const DataBase, EventKeywords, (EventKeywords)1
			public const Diagnostic, EventKeywords, (EventKeywords)2
			public const Perf, EventKeywords, (EventKeywords)4
		endclass

		public class Tasks
			public const Materialization, EventTask, (EventTask)2;
		endclass

		public method HarmonyFileIOMetricsEventSource
		proc
			ioopsCounter = new EventCounter("ioopsCounter", this)
			recordsCounter = new EventCounter("recordsCounter", this)
		endmethod

		public method ReportIOOPS, void
			ioops, int
		proc
			ioopsCounter.WriteMetric((float)ioops)
		endmethod

		public method ReportRecordsReturned, void
			records, int
		proc
			recordsCounter.WriteMetric((float)records)
		endmethod

		{EventAttribute(1, Opcode = EventOpcode.Start, Task = HarmonyFileIOMetricsEventSource.Tasks.Materialization, Keywords = HarmonyFileIOMetricsEventSource.Keywords.DataBase, Level = EventLevel.Informational)}
		public method MaterializationStart, void
			queryIdent, @string  
		proc
			WriteEvent(1, queryIdent) 
		endmethod

		{EventAttribute(2, Opcode = EventOpcode.Stop, Task = HarmonyFileIOMetricsEventSource.Tasks.Materialization, Keywords = HarmonyFileIOMetricsEventSource.Keywords.DataBase, Level = EventLevel.Informational)}
		public method MaterializationEnd,void
		proc
			WriteEvent(2)
		endmethod

	endclass

endnamespace
