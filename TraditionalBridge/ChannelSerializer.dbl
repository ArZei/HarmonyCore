namespace Json
	public enum SerializerStatus
		Ok
	endenum

	public enum SerializerStackType
		None,
		Object,
		Array
	endenum

	class ChannelSerializer
		private channel, i4
		private buffer, @string
		;;TODO: make a real stack out of a dynamic array or something like that
		private serializerStack, @System.Collections.ArrayList
		;;TODO: buffer writes into an a65535 instead of calling write directly
		;;TODO: validation 
		private expectingName, boolean, true
		private first, boolean, true

.define STACK_STATE, ((SerializerStackType)serializerStack[serializerStack.Count - 1])
.define STACK_STATE_ARRAY, (STACK_STATE == SerializerStackType.Array)
		public method ChannelSerializer
			chan, i
		proc
			buffer = ""
			channel = chan
			serializerStack = new System.Collections.ArrayList()
			serializerStack.Add((@SerializerStackType)SerializerStackType.None)
		endmethod

		private method Push, void
			req in type, SerializerStackType
		proc
			serializerStack.Add((@SerializerStackType)type)
		endmethod

		private method Pop, SerializerStackType
			record
				stackResult, SerializerStackType
		proc
			stackResult = STACK_STATE
			serializerStack.RemoveAt(serializerStack.Count - 1)
			mreturn stackResult
		endmethod

		
		public method Integer, SerializerStatus
			req in value, n
			record
				inArray, boolean
		proc
			inArray = STACK_STATE_ARRAY
			if(!first && inArray)
				buffer += ","
			first = false

			buffer += %string(value)

			if(!inArray)
				expectingName = true

			mreturn SerializerStatus.Ok
		endmethod

		public method Double, SerializerStatus
			req in value, id
			record
				inArray, boolean
		proc
			inArray = STACK_STATE_ARRAY
			if(!first && inArray)
				buffer += ","
			first = false

			buffer += %string(value)

			if(!inArray)
				expectingName = true

			mreturn SerializerStatus.Ok
		endmethod

		public method Null, SerializerStatus
			record
				inArray, boolean
		proc
			inArray = STACK_STATE_ARRAY
			if(!first && inArray)
				buffer += ","
			first = false

			buffer += "null"

			if(!inArray)
				expectingName = true

			mreturn SerializerStatus.Ok
		endmethod

		public method Bool, SerializerStatus
			req in value, boolean
			record
				inArray, boolean
		proc
			inArray = STACK_STATE_ARRAY
			if(!first && inArray)
				buffer += ","

			if(value) then
				buffer += "true"
			else
				buffer += "false"
			first = false

			if(!inArray)
				expectingName = true

			mreturn SerializerStatus.Ok
		endmethod

		public method String, SerializerStatus
			req in value, @string
			record
				inArray, boolean
		proc
			inArray = STACK_STATE_ARRAY
			if(!first && (expectingName || inArray))
				buffer += ","
			
			first = false

			;;TODO this string might need to be encoded, it probably shouldnt have binary data in it
			buffer += '"' + value + '"'
			if(expectingName)
			begin
				buffer += ":"
			end
			
			if(inArray)
				expectingName = !expectingName

			mreturn SerializerStatus.Ok
		endmethod

		public method MapOpen, SerializerStatus
		proc
			if(!first && STACK_STATE_ARRAY)
				buffer += ","
			expectingName = true
			first = true
			Push(SerializerStackType.Object)
			buffer += "{"
			mreturn SerializerStatus.Ok
		endmethod

		public method MapClose, SerializerStatus
			record
				bufferLength, d10
		proc
			first = false
			Pop()
			expectingName = !STACK_STATE_ARRAY
			buffer += "}"
			if(serializerStack.Count == 1)
			begin
				bufferLength = buffer.Length
				puts(channel, ^a(bufferLength))
				puts(channel, buffer)
				buffer = ""
			end
			mreturn SerializerStatus.Ok
		endmethod

		public method ArrayOpen, SerializerStatus
		proc
			if(!first && STACK_STATE_ARRAY)
				buffer += ","

			first = true
			expectingName = false
			Push(SerializerStackType.Array)
			buffer += "["
			mreturn SerializerStatus.Ok
		endmethod

		public method ArrayClose, SerializerStatus
			record
				bufferLength, d10
		proc
			first = false
			Pop()
			expectingName = !STACK_STATE_ARRAY
			buffer += "]"
			if(serializerStack.Count == 1)
			begin
				bufferLength = buffer.Length
				puts(channel, ^a(bufferLength))
				puts(channel, buffer)
				buffer = ""
			end

			mreturn SerializerStatus.Ok
		endmethod
	endclass
endnamespace
